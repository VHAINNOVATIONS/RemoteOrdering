Cache for UNIX^INT^HUB RAD^~Format=Cache.S~^RAW
%RO on 15 Jun 2016   5:57 PM
RAHBIN3^INT^1^^0
RAHBIN3 ;LEIDOS/TCK- Remote Ordering - Radiology - Filer ; 6/12/16 11:44am
 ;;1.0;Radiology Orders Portability;;March 1, 2016;Build 46
 ; Reference to ^RADPT( supported by IA #5602
 ; Reference to NOW^%DTC supported by IA #10000
 ; Reference to ^RAO(75.1 supported by IA #5604
 ; Reference to ^OR(100 supported by IA #5196
 ; Reference to GETDLG^ORCD supported by IA #5493
 ; Reference to ^RARPT(74 supported by IA #5605
 ; Reference to NOW^XLFDT supported by IA #10103
 ; Reference to X7005^RADD3 supported by IA #5661
 ; Reference to FIELD^DID supported by IA #2052
 ;
 ; updated:  20100908
 ; 4839 - changed Amended Impression & Report text label hdrs.
 ;        added 3 subhdr lines. File into subfile 74.06 old
 ;        text data to trigger the AMENDED report headings.
 ; 4833 - Encrypted Esig now saved, defined as Verifing Doctor.
 ; 4076 - init Principal Clinic from Requesting Loc
 ; 5329 - Send Abnormal diag Provider Alert, if needed
 Q
 ;
 ; NOTES:
 ; RADTE = Examination date/time in FM format truncated to maximum of 4 decimal places.
 ; RADTI = 9'S complement of RADTE (9999999.9999-RADTE) used as subscript in ^RADPT( to force chronological order.
 ; CASE = case# a 'Unique' number given to an exam when it is registered (generated by CN^RAREG1) ; AKA short case#
 ; LCASE = long case# ; LCASE=$E(RADTE,4,5)_$E(RADTE,6,7)_$E(RADTE,2,3)_"-"_CASE
 ;
 ;      3 files are affected by Radiology Registration and Resulting:
 ; ^RADPT(     #70    -- RAD/NUC MED PATIENT FILE
 ; ^RARPT(     #74    -- RAD/NUC MED REPORTS FILE
 ; ^RAO(75.1   #75.1  -- RAD/NUC MED ORDERS FILE
 ;
 ;====================================================================
EN ;
 ;
 ;
 Q
PAT(RADARRY) ; create patient entry in RADPT
 K DR,DA,DIE,LROK,DLAYGO
 I $G(RADFN)'>0,$G(DFN)>0 S RADFN=DFN
 S LROK=1
 Q:$D(^RADPT(RADFN))
 N X,Y,DINUM,DIC,DLAYGO
 ;
 S LROK=1
 S X=RADFN,DINUM=RADFN,DIC(0)="L",DIC="^RADPT(",DLAYGO=2
 K RADFN
 S RADFN=X
 D FILE^DICN
 I RADFN'=+Y S LROK=0,RAOROUT(1)="-1^COULD NOT CREATE RECORD IN RADPT" Q
 ;
 S DR=".06////"_DUZ
 S DA=RADFN,DIE=70 K Y D ^DIE
 ;
 Q
 ;========================================================== 
REG(RADARRY) ; register exam in RADPT (#70)  & RAO(75.1  (#75.1)
 ; Input:
 ;
 N RADFDA,RAIEN,ERRMSG,NOW,REQSTA,RAONWSTA,%,DT1
 D NOW^%DTC S NOW=%
 S RADARRY("REQSTA")=6,RADARRY("RAONWSTA")="6"
 ;
 ;    file #70.02:    Registered Exams
 S DT1=RADTI
 K DFN
 S DFN=RADFN
 S RAIEN(1)=DT1
 S RADFDA(70.02,"+1,"_RADFN_",",.01)=RADTE      ; exam date/time
 S RADFDA(70.02,"+1,"_RADFN_",",2)=RADARRY("IMGTYP")   ; type of image=>79.2
 S RADFDA(70.02,"+1,"_RADFN_",",3)=RADARRY("HOSDIV")   ; hospital division=>79
 S RADFDA(70.02,"+1,"_RADFN_",",4)=RADARRY("IMGLOC")   ; imaging location=>79.1
 ;
 D UPDATE^DIE("S","RADFDA","RAIEN","ERRMSG")
 I $G(DIERR) D ERR
 ;    file #70.03:    Examinations
 K RADFDA,ERRMSG
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",.01)=RADARRY("CASE")   ; case#
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",2)=RADARRY("PROC")     ; procedure=>71
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",3)=RADARRY("EXSTAT")   ; exam status=>72
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",4)=RADARRY("EXCAT")    ; category of exam:codes
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",6)=RADARRY("WARD")     ; ward=>42
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",7)=RADARRY("SERVC")    ; service=>49
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",8)=RADARRY("REQLOC")  ;4076 dupe in Reqloc ptr=>44
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",11)=RADARRY("RAOIEN")  ; imaging order=>75.1
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",14)=RADARRY("REQPRV")  ; requesting provider=>200
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",19)=RADARRY("BED")     ; bedsection=>42.4
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",21)=RADARRY("REQDT")   ; request date
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",22)=RADARRY("REQLOC")  ; request location=>44
 S RADFDA(70.03,"+2,"_RADTI_","_RADFN_",",26)=RADARRY("CRDMTH")  ; credit method: codes
 ;
 D UPDATE^DIE("S","RADFDA","","ERRMSG")
 I $G(DIERR) D ERR
 ;
 ;   get DA2
 N RAOIEN S RAOIEN=RADARRY("RAOIEN") ; 20110329
 N DA2,FOUND S FOUND=0 ; 20110329
 S DA2=0,DA2=$O(^RADPT(RADFN,"DT",RADTI,"P",DA2)) Q:FOUND  ; 20110329
 Q:'DA2
 I $P(^RADPT(RADFN,"DT",RADTI,"P",DA2,0),"^",11)=RAOIEN S FOUND=1 ; 20110329
 I 'FOUND S DA2=0 ; 20110329
 ;
 ;Need to update 70.05 exam status change history
 I $G(DA2) D  ; 20110329
 . K RADFDA,ERRMSG
 . ;D A7007^RADD3(RADFN,RADTI,DA2,RADARRY("DUZ"),$G(RADARRY("TECH")))
 . S RADFDA(70.07,"+1,"_DA2_","_RADTI_","_RADFN_",",.01)=$$NOW^XLFDT
 . S RADFDA(70.07,"+1,"_DA2_","_RADTI_","_RADFN_",",2)="E"
 . S RADFDA(70.07,"+1,"_DA2_","_RADTI_","_RADFN_",",3)=$G(RADARRY("DUZ"))
 . S RADFDA(70.07,"+1,"_DA2_","_RADTI_","_RADFN_",",4)=$G(RADARRY("TECH"))
 . D UPDATE^DIE("S","RADFDA","","ERRMSG") K RADFDA,ERRMSG
 . S EXSTAT=RADARRY("EXSTAT")
 . D X7005^RADD3(RADFN,RADTI,DA2,1,"",EXSTAT,RADARRY("DUZ"))
 . ;S RAIENS="+1,"_DA2_","_RADTI_","_RADFN_","
 . ;S RADFDA(70.05,RAIENS,.01)=$$MIDNGHT^RAUTL5($$NOW^XLFDT()) ; 20110329
 . ;D UPDATE^DIE(,"RADFDA","RAIENS","ERRMSG")
 . ;S RAIENS="+1,"_DA2_","_RADTI_","_RADFN_","
 . ;S RADFDA(70.05,RAIENS,2)=EXSTAT ; 20110329
 . ;S RADFDA(70.05,RAIENS,3)=RADARRY("DUZ") ; 20110329
 . ;D UPDATE^DIE(,"RADFDA","RAIENS","ERRMSG") K RADFDA,ERRMSG
 . ;D UPDATE^DIE("S","RADFDA","","ERRMSG") K RADFDA,ERRMSG
 . B
 . I $D(PMOD) D FLEMOD(.PMOD)
 . I $G(DIERR) D ERR
 ;    log activity in RAO(75.1
 K RADFDA,ERRMSG
 S RADFDA(75.1,RAOIEN_",",5)=RADARRY("REQSTA")      ; request status:codes
 I SEX="F" S RADFDA(75.1,RAOIEN_",",13)=RADARRY("PRGFLG")     ; preg flag ; rhl 20100927
 S RADFDA(75.1,RAOIEN_",",18)=RADARRY("NOW")     ; last activity d/t
 S RADFDA(75.12,"+1,"_RAOIEN_",",.01)=RADARRY("NOW")   ; status change d/t
 S RADFDA(75.12,"+1,"_RAOIEN_",",2)=RADARRY("RAONWSTA")    ; new status:codes
 S RADFDA(75.12,"+1,"_RAOIEN_",",3)=RADARRY("VRFPHY")  ; computer user=>200
 ;
 D UPDATE^DIE("S","RADFDA","","ERRMSG")
 I $G(DIERR) D ERR
 ;
 ;Pregnancy update in 100 file 4.5 node (Dialogue)
 ;convert from lower case to upper case
 ;First check to see if Rad status is set in OR(100 file
 K PRGSEQ,PRGDONE,LSTSEQ,PRGSEQ,LSTSEQ,PRGTEXT1,PRGTEXT2,PRGTEXT
 S PRGSEQ=5,PRGDONE=0  ;Never going to have less than a 6 for pregnancy sequence
 S ORIEN=$G(RADARRY("ORIEN"))
 I $G(SEX)="F" D
 . F  S PRGSEQ=$O(^OR(100,ORIEN,4.5,PRGSEQ)) Q:PRGSEQ'=+PRGSEQ  D
 . . I ^OR(100,ORIEN,4.5,PRGSEQ,0)["PREGNANT" S PRGDONE=1
 . . S LSTSEQ=PRGSEQ
 . I PRGDONE=0 D
 . . N ORDIALOG
 . . K PRGFLG
 . . S PRGFLG=RADARRY("PRGFLG")
 . . S PRGFLG=$TR(PRGFLG,"nyu","NYU")
 . . D GETDLG^ORCD(1)
 . . S PRGTEXT1=$P(ORDIALOG("B","PREGNANT"),"^",2)
 . . S PRGTEXT2=$P(ORDIALOG(PRGTEXT1),"^")
 . . K ORDIALOG
 . . N RAIEN,RADFDA,RADFILE
 . . S RADFILE=100.045,RAIEN=ORIEN_","
 . . S RADFDA(1,RADFILE,"+1,"_RAIEN,.01)=PRGTEXT2
 . . S RADFDA(1,RADFILE,"+1,"_RAIEN,.02)=PRGTEXT1
 . . S RADFDA(1,RADFILE,"+1,"_RAIEN,.03)="1"
 . . S RADFDA(1,RADFILE,"+1,"_RAIEN,.04)="PREGNANT"
 . . S RADFDA(1,RADFILE,"+1,"_RAIEN,1)=PRGFLG
 . . D UPDATE^DIE("S","RADFDA(1)","","ERRMSG") K RADFDA,RADFILE,RAIEN
 ;
REGQ ; 
 Q
 ;==========================================================
 ;
FLEMOD(PMOD) ;
 Q:'$D(PMOD)
 S IDX="" F  S IDX=$O(PMOD(IDX)) Q:IDX=""  D
 .Q:$G(PMOD(IDX))=""
 .S MOD=PMOD(IDX) K PMOD(IDX)
 .;Q:$D(^RADPT(RADFN,"DT",RADTI,"P",1,"M","B",MOD))
 .S RADARRY("PROCMOD")=MOD
 .S RADFDA(70.1,"+1,"_DA2_","_RADTI_","_RADFN_",",.01)=RADARRY("PROCMOD")
 .D UPDATE^DIE("S","RADFDA","","ERRMSG")
 .S ORIEN=$G(RADARRY("ORIEN"))
 .Q:$$CHKMOD(ORIEN,MOD)
 .N ORDIALOG
 .K PROCMOD
 .S PROCMOD=RADARRY("PROCMOD")
 .D GETDLG^ORCD(1)
 .S PROCMOD1=$P(ORDIALOG("B","PROCEDURE MODIFIER"),"^",2)
 .S PROCMOD2=$P(ORDIALOG(PROCMOD1),"^")
 .S I=$O(^OR(100,ORIEN,4.5,"ID","MODIFIER",999),-1)
 .S I=$G(I)+1
 .K ORDIALOG
 .N RAIEN,RADFDA,RADFILE
 .S RADFILE=100.045,RAIEN=ORIEN_","
 .S RADFDA(1,RADFILE,"+1,"_RAIEN,.01)=PROCMOD2
 .S RADFDA(1,RADFILE,"+1,"_RAIEN,.02)=PROCMOD1
 .S RADFDA(1,RADFILE,"+1,"_RAIEN,.03)=$G(I)
 .S RADFDA(1,RADFILE,"+1,"_RAIEN,.04)="MODIFIER"
 .S RADFDA(1,RADFILE,"+1,"_RAIEN,1)=RADARRY("PROCMOD")
 .D UPDATE^DIE("S","RADFDA(1)","","ERRMSG") K RADFDA,RADFILE,RAIEN
 Q
 ;
CHKMOD(ORIEN,MOD) ;
 N N
 S FOUND=0,N=""
 Q:'$D(^OR(100,ORIEN,4.5,"ID","MODIFIER"))
 F  S N=$O(^OR(100,ORIEN,4.5,"ID","MODIFIER",N)) Q:N=""  D  Q:FOUND
 .Q:$P(^OR(100,ORIEN,4.5,N,0),"^",4)'["MOD"
 .Q:'$D(^OR(100,ORIEN,4.5,N,1))
 .I MOD=^OR(100,ORIEN,4.5,N,1) S FOUND=1
 Q FOUND
 ;
RPT(RADARRY) ; file results in RADPT (#70), RARPT (#74)
 ; Input: CASE, RAOIEN, ORIEN
 N RADTI,DA2,NOW,ACTION,ERROR
 D NOW^%DTC S NOW=%
 S RADARRY("RPTSTA")="V",ERROR=0
 ; Input:
 ;
 ;
 ;bsl; add code for amending results
 ;Must check to see if results have already been filed
 K AMEND,EXRSLT
 D GETDPT I 'LROK S RAOROUT(1)="-1^COULD NOT FIND RECORD TO POST RESULTS" Q
 S EXRSLT=^RADPT(RADFN,"DT",RADTI,"P",DA2,0)
 S RPTIEN=+$P(EXRSLT,"^",17)     ;convert to number
 ;
 ;* Report amendment process, Do IF ien non 0 and then quit filer *
RPTIEN ;
 I RPTIEN D  Q
 . N II
 . D RPTERR    ;4839-save prev text fields first/Error Reports subfile
 . ;
 . ;4839:
 . ;save incoming text fields to RARPT file #74
 . ;        Impression amended text
 . I $D(WPIT("WP")) D
 . . S AMEND("WP",1)=""
 . . S AMEND("WP",2)="***Addendum Below***"
 . . ;insert blank if needed
 . . S II=$O(WPIT("WP",0))
 . . S:$G(WPIT("WP",II))]"" AMEND("WP",3)=""
 . . D WP^DIE(74,RPTIEN_",",300,"A","AMEND(""WP"")","ERRMSG")
 . . D WP^DIE(74,RPTIEN_",",300,"A","WPIT(""WP"")","ERRMSG")
 . . K AMEND("WP")
 . ;        Report amended text
 . I $D(WPRT("WP")) D
 . . S AMEND("WP",1)=""
 . . S AMEND("WP",2)="***Addendum Below***"
 . . ;insert blank if needed
 . . S II=$O(WPRT("WP",0))
 . . S:$G(WPRT("WP",II))]"" AMEND("WP",3)=""
 . . D WP^DIE(74,RPTIEN_",",200,"A","AMEND(""WP"")","ERRMSG")
 . . D WP^DIE(74,RPTIEN_",",200,"A","WPRT(""WP"")","ERRMSG")
 . ;update Report DT & Verify DT & xref to show the amended DOD report
 . ;time - 4839
 . N OLDVTIM,VNOW
 . S OLDVTIM=$P(^RARPT(RPTIEN,0),U,7),OLDVTIM=9999999.9999-OLDVTIM
 . K ^RARPT("AA",OLDVTIM,RPTIEN)
 . S VNOW=$E($$NOW^XLFDT,1,12)
 . S $P(^RARPT(RPTIEN,0),U,7)=VNOW
 . S $P(^RARPT(RPTIEN,0),U,8)=VNOW
 . S ^RARPT("AA",9999999.9999-VNOW,RPTIEN)=""
 . D END
 ;* End of Report amendment process *
 ;
 ;** Filing of original reports below **
 ;  file RARPT #74
 K RPTIEN,DIE,DA,DR
 N X,DIC,DLAYGO
 S X=LCASE,DIC(0)="L",DIC="^RARPT(",DLAYGO=2
 D FILE^DICN S RPTIEN=+Y ; create new report record file# 74
 S RADARRY("RPTIEN")=RPTIEN
 ;
 K RADFDA,ERRMSG,LCASE
 S RADFDA(74,RPTIEN_",",2)=RADARRY("DFN")      ; patient=>2
 S RADFDA(74,RPTIEN_",",3)=RADARRY("EXMDT")    ; exam dt
 S RADFDA(74,RPTIEN_",",4)=RADARRY("CASE")     ; case#
 S RADFDA(74,RPTIEN_",",5)=RADARRY("RPTSTA")   ; report status:codes
 S RADFDA(74,RPTIEN_",",6)=RADARRY("RPTENTDT") ; date report entered
 S RADFDA(74,RPTIEN_",",7)=RADARRY("VRFDT")    ; verified date
 S RADFDA(74,RPTIEN_",",8)=RADARRY("RPTDT")    ; reported date
 S RADFDA(74,RPTIEN_",",9)=RADARRY("VRFPHY")   ; verifying physician=>200
 ;4833 dot structure
 D:RADARRY("VRFPHY")   ;encrpyt esig from RARPT ien, doc duz, doc name
 . S X2=RPTIEN,X1=RADARRY("VRFPHY"),X=$P($$ESBLOCK^XUSESIG1(X1),U,1)
 . D EN^XUSHSHP S RADARRY("ESCD")=X
 S RADFDA(74,RPTIEN_",",10)=RADARRY("ESCD")    ; electronic signature code
 S RADFDA(74,RPTIEN_",",11)=RADARRY("TRNSCP")  ; transcriptionist=>200
 S RADFDA(74,RPTIEN_",",17)=RADARRY("SCVBY")   ; status changed to verified by=>200
 S RADFDA(74,RPTIEN_",",86)=RADARRY("ITRLOC")  ; interpreting imaging location=>79.1
 ;
 K FLAGS
 S FLAGS=""
 D FILE^DIE(FLAGS,"RADFDA","ERRMSG")
 I $G(DIERR) D ERR
 ;
 K RADFDA,ERRMSG
 S RADFDA(74.01,"+1,"_RPTIEN_",",.01)=RADARRY("NOW") ; log date
 S RADFDA(74.01,"+1,"_RPTIEN_",",2)=RADARRY("ACTION")    ; type of action
 S RADFDA(74.01,"+1,"_RPTIEN_",",3)=RADARRY("DUZ")       ; computer user
 D UPDATE^DIE("S","RADFDA","","ERRMSG")
 I $G(DIERR) D ERR
 ;     file word processing data
 I $D(WPACH("WP")) D WP^DIE(74,RPTIEN_",",400,"","WPACH(""WP"")","ERRMSG")  ; additional clinical history
 I $D(WPIT("WP")) D WP^DIE(74,RPTIEN_",",300,"","WPIT(""WP"")","ERRMSG")     ; impression text
 I $D(WPRT("WP")) D WP^DIE(74,RPTIEN_",",200,"","WPRT(""WP"")","ERRMSG")     ; report text
 ;
 ;  file results into RADPT( exminations
 ;    file #70.03:
 K RADFDA,ERRMSG,PROC
 S FLAGS=""
 ;BSL;Set exam status to complete
 S RADARRY("EXSTAT")=$$GETSTA^RAHBIN2(RADARRY("PROC"),"COMPLETE")
 ;
 ;S RADFDA(70.03,DA2_","_RADTI_","_RADFN_",",12)=PRIRES   ; primary interpreting resident=>200
 S RADFDA(70.03,DA2_","_RADTI_","_RADFN_",",13)=RADARRY("PRIDGC")   ; primary diagnosis code=>74
 S RADFDA(70.03,DA2_","_RADTI_","_RADFN_",",15)=RADARRY("PRISTF")   ; primary interpreting staff=>200
 S RADFDA(70.03,DA2_","_RADTI_","_RADFN_",",17)=RADARRY("RPTIEN")   ; report text=>74
 S RADFDA(70.03,DA2_","_RADTI_","_RADFN_",",3)=RADARRY("EXSTAT")  ;EXAM STATUS
 S FLAGS=""
 D FILE^DIE(FLAGS,"RADFDA","ERRMSG")
 I $G(DIERR) D ERR
 D X7005^RADD3(RADFN,RADTI,DA2,"","",RADARRY("EXSTAT"),$G(RADARRY("DUZ")))
 D A7007^RADD3(RADFN,RADTI,DA2,RADARRY("DUZ"),$G(RADARRY("TECH")))
 ;Update 100 File
 ;BL;Modified to update status regardless of errors, response to JIRA 2048
 ;I 'ERROR,$D(ORIEN) D removing dot structure to ensure code gets called jira 2048
 ;file new proc mods if found
 ;BL;IA compliance change
 S ORIEN=RADARRY("ORIEN")
 N NOW
 S NOW=$$NOW^XLFDT,FLAGS=""
 S RADFDA(100,ORIEN_",",22)=NOW
 S RADFDA(100,ORIEN_",",5)=2
 D FILE^DIE(FLAGS,"RADFDA","ERRMSG")
 ;add remote user's name to the 8 node of file 100
 I $D(^OR(100,ORIEN,8,1,.1)) D
 .S CNT=$P(^OR(100,ORIEN,8,1,.1,0),"^",3)
 .S CNT=CNT+1,$P(^OR(100,ORIEN,8,1,.1,0),"^",2,3)=CNT
 .S ^OR(100,ORIEN,8,1,.1,CNT,0)="Verified By: "_RADARRY("USER")
 I $G(DIERR) D ERR
 ;UPDATE 75.1 with new status
 S RADFDA(75.1,RAOIEN_",",5)=RADARRY("EXSTAT")
 S RADFDA(75.1,RAOIEN_",",18)=RADARRY("NOW")
 S RADFDA(75.12,"+1,"_RAOIEN_",",.01)=RADARRY("NOW")
 S RADFDA(75.12,"+1,"_RAOIEN_",",2)=RADARRY("EXSTAT")
 S RADFDA(75.12,"+1,"_RAOIEN_",",3)=RADARRY("REQPRV")
 D UPDATE^DIE("S","RADFDA","","ERRMSG")
 ;
 ;BL;UPDATE IMAGING LOCATION WHEN RESULTS COME IN; ISSUE 1937
 S RADFDA(70.02,"+1,"_RADFN_",",4)=RADARRY("IMGLOC")   ; imaging location=>79.1
 S FLAGS=""
 D FILE^DIE(FLAGS,"RADFDA","ERRMSG")
 I $G(DIERR) D ERR
 ;
END ;Check if Abnormal Alert needed then, finish cleanup    ;5329
 ;
 ;setup variables for Abnormal alert testing & conditional call, ;5329
 N RAAB,RACNI,RAEXAM0,RARPT
 S RACNI=$O(^RADPT(RADFN,"DT",RADTI,"P","B",RADARRY("CASE"),0))
 S RAEXAM0=$S($D(^RADPT(RADFN,"DT",RADTI,"P",RACNI,0)):^(0),1:0)
 S RARPT=RPTIEN    ;Glb Var needed by OE3, file 74 ien
 I $G(RADARRY("PRIDGC"))>0 D
 .S RAAB=$S($P(^RA(78.3,+RADARRY("PRIDGC"),0),U,4)="y":1,1:0)
 I $G(RAAB)>0 D OE3^RAUTL00(RADFN,RADTI,RACNI,RAEXAM0)
 ;
 ;cleanup
 K RIEN,WPACH,WPIT,WPRT,XMSG,RAOIEN,RADTE,RADFDA,RACNT,PMOD,SECDGC,PRIDGC
 ;
RPTQ ;
 Q
 ;
GETDPT ; get RADTI  and DA2 for a Case# & orien
 N STOP
 S RADTI=0,DA2="",STOP=0,LROK=0
 F  S RADTI=$O(^RADPT(RADFN,"DT",RADTI)) Q:'RADTI  D  Q:STOP
 . S DA2=0,DA2=$O(^RADPT(RADFN,"DT",RADTI,"P",DA2))
 . I RADARRY("CASE")=+^RADPT(RADFN,"DT",RADTI,"P",DA2,0),$P(^RADPT(RADFN,"DT",RADTI,"P",DA2,0),"^",11)=RAOIEN S STOP=1,LROK=1
 ; sub-sub files
 ;F I=1:1 Q:'$D(SECRES(I))  D  ; secondary interpreting resident=>200
 ;. K RADFDA,ERRMSG
 ;F I=1:1 Q:'$D(SECSTF(I))  D  ; secondary interpreting staff=>200
 ;. K RADFDA,ERRMSG
 ;. S RADFDA(70.11,"+1,"_DA2_","_RADTI_","_RADFN_",",.01)=SECSTF(I)
 ;. D UPDATE^DIE("S","RADFDA","","ERRMSG")
 Q:'$D(SECDGC)
 F I=1:1 Q:'$D(SECDGC(I))  D  ; secondary diagnosis code=>78.3
 .K RADFDA,ERRMSG
 .S RADFDA(70.14,"+1,"_DA2_","_RADTI_","_RADFN_",",.01)=SECDGC(I)
 .D UPDATE^DIE("S","RADFDA","","ERRMSG")
 Q
 ;
ERR ; return errors from filer
 S ERROR=1
 ;I $G(DEBUG) D DGERR^JVDG2("JVDGF",DBCNT,.XMSG,"F") ; debug log
 N XO,XF,XL S XO="" F  S XO=$O(ERRMSG("DIERR",XO)) Q:'XO  D
 . S XF=$G(ERRMSG("DIERR",XO,"PARAM","FIELD")),XL=$G(ERRMSG("DIERR",XO,"PARAM","FILE")) Q:((XF="")!(XL=""))
 . S XF=$$DDLBL(XL,XF)
 . S RACNT=RACNT+1,RAOROUT(RACNT)=XF_": "_$G(XMSG("DIERR",XO,"TEXT",1))
 ;
 K DIERR,ERRMSG
 Q
 ;
DDLBL(FILE,FLD) ; return field name. (This entry point copied from JVDG, since we're in a separate package.)
 ; Input:
 ;    FILE=file#
 ;    FLD=Field number
 ;  Returns
 ;    field's Label
 I $G(FILE)=""!($G(FLD)="") Q ""
 N FDA
 D FIELD^DID(FILE,FLD,"","LABEL","FDA","")
 Q $G(FDA("LABEL"))
 ;
RPTERR ;Saves previous text fields to subfile #74.06 to trigger the Amended 
 ;report headings.  4839.
 ;
 N QQ,CH,RT,IT,ECNT
 M CH=^RARPT(RPTIEN,"H") K CH(0)
 M RT=^RARPT(RPTIEN,"R") K RT(0)
 M IT=^RARPT(RPTIEN,"I") K IT(0)
 ;build Tmp report with old values for #74.06
 S ECNT=1
 S ^TMP($J,"RA AUTOE",ECNT)="** DOD incoming Radiology Results event **"
 S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)=""
 I $D(CH) D
 . S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)="  Additional Clinical History:"
 . F QQ=0:0 S QQ=$O(CH(QQ)) Q:'QQ  D
 . . S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)=CH(QQ,0)
 I $D(RT) D
 . S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)="  Report:"
 . F QQ=0:0 S QQ=$O(RT(QQ)) Q:'QQ  D
 . . S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)=RT(QQ,0)
 I $D(IT) D
 . S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)="  Impression:"
 . F QQ=0:0 S QQ=$O(IT(QQ)) Q:'QQ  D
 . . S ECNT=ECNT+1,^TMP($J,"RA AUTOE",ECNT)=IT(QQ,0)
 D EN1^RARTE3(RPTIEN)    ;call to legacy to file ERR nodes in TMP
 Q
 ;



