Cache for UNIX^INT^HUBIN^~Format=Cache.S~^RAW
%RO on 25 Mar 2016  12:29 PM
LRHBIN^INT^1^^0
LRHBIN ;;LEIDOS/TCK - REMOTE ORDER - INCOMING ; 3/25/16 11:26am
 ;;1.0;REMOTE ORDERS;;OCT 1,2016;
 ; Reference to UNESC^ORHLESC supported by IA #4922
 ;; TEST
 ;
EN ;
 ; Entry point for INCOMING ORDERS FROM A HOST FACILITY
 N I,D1,D2,D3,D4,D5,TYPE
 S (D1,D2,D3,D4,D5,TYPE)="",C=0  ;Initialize varieables
 I $G(HLMTIEN)'>0 S LROROUT(1)="0^No Results Array Received" Q
 S HLMA="",HLMA=$O(^HLMA("B",HLMTIEN,HLMA))
 Q:$G(HLMA)'>0
 Q:'$D(^HLMA(HLMA,0))
 Q:'$D(^HLMA(HLMA,"MSH"))
 I $P(^HLMA(HLMA,"MSH",1,0),"|",3)["LA7V" S TYPE="LAB"
 I $P(^HLMA(HLMA,"MSH",1,0),"|",3)["RA REMOTE" S TYPE="IMAGING"
 S LOC=+$P(^HLMA(HLMA,"MSH",1,0),"|",4)
 M ^TMP("RMTE",1)=^HLMA(HLMA,"MSH",1,0)
 S I=0 F  S I=$O(^HL(772,HLMTIEN,"IN",I)) Q:I=""  D
 .S C=$G(I)+1
 .S ^TMP("RMTE",C)=^HL(772,HLMTIEN,"IN",I,0)
 M REC=^TMP("RMTE")
 Q:'$D(REC)
 D EN^LRHBIN1(.REC,TYPE,LOC)
 Q
 ;
TEST(LROROUT,HLMTIEN) ;
 D EN
 Q
 ;
GTDOM(R) ;
 N NME,INST,CNT
 S (DOM,INST,R)="",CNT=0
 Q:'$D(^DIC(4,"E"))
 S INST="" F  S INST=$O(^DIC(4,"E",INST)) Q:INST=""  D
 .S NME=$$GET1^DIQ(4,INST,.01,"E")
 .S CNT=$G(CNT)+1
 .S R(CNT)=INST_"^"_NME
 Q
 ;
DC(REC,ORID,ORVP,ORNP,ORL,REAS,DCORIG,ISNEWORD) ;
 S REMOTE=1
 N FLG,FLAG,LRSEND,LRODT,LRSN,LRSEND,ORN,LRIEN,LRFILE,LRAA,LRAN,LRAD
 S (FLG,FLAG)=0,(LRAA,LRAN,LRAD)=""
 S (DCORIG,ISNEWORD)=0
 S ORDNUM=+ORID
 S FILE=100,ORIEN=ORDNUM_","
 I $$GET1^DIQ(FILE,ORIEN,31,"I") D  Q:FLG
 .I $$GET1^DIQ(FILE,ORIEN,5,"I")=1 S NDE=$O(^OR(100,ORDNUM,8,9999),-1),REC(1)=ORDNUM_";"_NDE_"^RS" S FLG=1 Q
 Q:FLG
 S STATUS=$$GET1^DIQ(100,ORDNUM_",",5,"I")
 S PTYPE=$$GET1^DIQ(100,ORDNUM_",",12)
 ;4900 send error msg for a Lab DC try for a completed order
 I STATUS=2&(PTYPE'["RAD") D  Q
 .S REC(1)="Warning: Not cancelled. Lab order is already completed."
 I PTYPE'["RAD" D
 .S LRFILE=100,LRIEN=ORDNUM_","
 .S LRDATA=$$GET1^DIQ(LRFILE,LRIEN,33,"I") I LRDATA="" Q
 .S LRORD=$P(LRDATA,";"),LRODT=$P(LRDATA,";",2),LRSND=$P(LRDATA,";",3)
 .Q:$G(LRORD)=""
 .Q:$G(LRODT)=""
 .Q:$G(LRSND)=""
 .I '$D(^LRO(69,LRODT,1,LRSND)) Q
 .I '$D(^LRO(69,LRODT,1,LRSND,2)) Q
 .S LRSEND=0 F  S LRSEND=$O(^LRO(69,LRODT,1,LRSND,2,LRSEND)) Q:LRSEND'?.N  D
 ..S ORN=$P(^LRO(69,LRODT,1,LRSND,2,LRSEND,0),U,7)
 ..I ORN=ORDNUM D
 ...S LRSN=LRSND,LRI=LRSEND,ORIFN=ORN
 ...S LRTEST=$P(^LRO(69,LRODT,1,LRSND,2,LRSEND,0),U)
 ...S LRSS=$$GET1^DIQ(60,LRTEST,4,"I")
 ...S LIEN=LRSN_","_LRODT_","
 ...S LRDFN=$$GET1^DIQ(69.01,LIEN,.01,"I")
 ...S PNM=$$GET1^DIQ(2,ORVP,.01,"I"),LRDPF=2
 ...S LRAA=$P(^LRO(69,LRODT,1,LRSND,2,LRSEND,0),U,4)
 ...S LRAD=$P(^LRO(69,LRODT,1,LRSND,2,LRSEND,0),U,3)
 ...S LRAN=$P(^LRO(69,LRODT,1,LRSND,2,LRSEND,0),U,5)
 .I LRAA'="",LRAD'="",LRAN'="" S FLAG=1
 I REAS?.N D
 .D DC^ORWDXA(.REC,ORID,ORNP,ORL,REAS,DCORIG,ISNEWORD)
 .D BLDSND^LRHBIN1(.REC,.ORWREC)
 .K REC S ES="AAA",ORWLST=""
 .D SEND^ORWDX(.ORWLST,ORVP,ORNP,ORL,ES,.ORWREC)
 .M REC=ORWLST K ORWLST
 I REAS'?.N D
 .N DEF,R,PKG,X,OK
 .S PKG=$O(^DIC(9.4,"B","LAB SERVICE",0))
 .S DEF=$P($G(^LAB(69.9,1,"OR")),"^",2)
 .S X=$$DC^ORX1(DEF,+$G(REQ),PKG,"Cancellation Reason"),LRNATURE=$S(X:"^^^"_$P(X,"^",1,2)_"^99ORR",1:-1)
 .S LRSN=LRSND
 .S LRTNM=$$GET1^DIQ(60,LRTEST,.01,"I")
 .S LRCCOM=$G(REAS)
 .S ORIFN=ORN,LRTSTS=LRTEST
 .D ^LRPARAM
 .;Call LRTSTOUT to update file 69, 68 and 63
 .D SET^LRTSTOUT
 I '$D(REC(1)) S REC(1)=ORID_"^RS"
 Q
 ;
CAN69(LRTEST,ORIFN,LRODT,LRSND,LRSEND) ;
 S LRNOW=$$NOW^XLFDT
 I $G(REMOTE)=1 D
 .N II,JVACC,JVDT,JVXREF,LRDFN,LRIDT,LRMSTATI,LRSTATUS,LRI
 .S X=1+$O(^LRO(69,LRODT,1,LRSND,2,LRSEND,1.1,"A"),-1),X(1)=$P($G(^(0)),U,4)
 .S LRNATURE="SERVICE CORRECTION",LRNOW=$$NOW^XLFDT
 .S LRCCOM="SERVICE CORRECTION.",LRSTATUS="",LRMSTATI="OC"
 .S ^LRO(69,LRODT,1,LRSND,2,LRSEND,1.1,X,0)=$P($G(LRNATURE),U,5)_": "_"*NP Action: "_$$FMTE^XLFDT(LRNOW,"1FMZ")_"  "_LRCCOM
 .S ^LRO(69,LRODT,1,LRSND,2,LRSEND,1.1,0)="^^"_X_"^"_X(1)_"^"_DT
 .I $G(ORIFN),$D(II) D NEW^LR7OB1(LRODT,LRSND,$S($G(LRMSTATI)=""!($G(LRMSTATI)=1):"OC",1:"SC"),$G(LRNATURE),.II,LRSTATUS)
 .S $P(^LRO(69,LRODT,1,LRSND,2,LRSEND,0),"^",9)="CA",$P(^(0),U,10)="L",$P(^(0),U,11)=$G(DUZ)
 .S:$D(^LRO(69,LRODT,1,LRSND,"PCE")) ^LRO(69,"AE",DUZ,LRODT,LRSND,LRSEND)=""
 ;DELETE MANAGEMENT REPORT CROSS REFERENCES
 ;S LRAD=$P($G(^LRO(69,LRODT,1,LRSND,2,LRSEND,0)),"^",3)
 ;S LRAA=$P($G(^LRO(69,LRODT,1,LRSND,2,LRSEND,0)),"^",4)
 ;S LRAN=$P($G(^LRO(69,LRODT,1,LRSND,2,LRSEND,0)),"^",5)
 S ORID=ORIFN
 D DLTRPT(ORID,LRAA,LRAD,LRAN,LRODT,LRSND)
 Q
 ;
DLTRPT(ORID,LRAA,LRAD,LRAN,LRODT,LRSND) ;
 ;IF LRAA IS NOT DEFINED, ORDER NOT ACCESSIONED SO QUIT
 I $G(LRAA)="" S JVOROUT(1)=ORIFN_";"_2_"^"_"RS" Q
 S LRDFN=$P($G(^LRO(68,LRAA,1,LRAD,1,LRAN,0)),"^")
 S JVACC=^LRO(68,LRAA,1,LRAD,1,LRAN,.2)
 S LRIDT=$P($G(^LRO(68,LRAA,1,LRAD,1,LRAN,3)),"^",5)
 Q:$G(JVACC)=""
 ;CHECK IF FILE 68 EXISTS FOR THIS RECORD, DELETE FILE 63.
 I $G(LRAA)>0 S SUCCESS=0 D CAN63(LRAA,LRAD,LRAN,LRDFN,LRSS,LRTEST,.SUCCESS)
 I $G(SUCCESS)>0 S REC(1)=ORID_"^RS"
 I $G(SUCCESS)'>0 S REC(1)="-1^Error: Discontinue failed." Q
 Q
 ;
CAN63(LRAA,LRAD,LRAN,LRDFN,LRSS,LRTEST,SUCCESS) ;
 N JV,LRI
 S JVIENS=LRAN_","_LRAD_","_LRAA_",",LRFILE=68.02
 S INVDT=$$GET1^DIQ(LRFILE,JVIENS,13.5,"I"),LRI=INVDT
 S JVIENS=LRTEST_","_LRAN_","_LRAD_","_LRAA_",",LRFILE=68.04
 S JVFDA(4,LRFILE,JVIENS,4)=$$NOW^XLFDT
 D UPDATE^DIE("","JVFDA(4)","JVIENS","JVMSG")
 S TEST=$$GET1^DIQ(60,LRTEST,.01)
 S PFMBY=$$GET1^DIQ(200,DUZ,.01)
 S TEXT="*"_$G(TEST)_" Not Performed: "_$$FMTE^XLFDT(LRNOW,"1FMZ")_" by "_$G(PFMBY)
 S LRFILE=$S(LRSS="CH":63.041,LRSS="MI":63.051,1:0)
 S FDA(2,LRFILE,"+2,"_INVDT_","_LRDFN_",",.01)=TEXT
 D UPDATE^DIE("","FDA(2)","","LRERR(2)")
 K FDA(1),LRDIE(1)
 D CLEAN^DILF
 Q
 ;



