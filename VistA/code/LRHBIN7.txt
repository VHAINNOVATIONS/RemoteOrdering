Cache for UNIX^INT^RESULTS IN^~Format=Cache.S~^RAW
%RO on 25 Mar 2016  12:34 PM
LRHBIN7^INT^1^^0
LRHBIN7 ;SAIC/TCK- Orders Portability - Laboratory Results - Filer ; 3/22/16 9:05am
 ;;1.0;Orders Portability;;March 1, 2010;Build 114
 ;
 Q
RSLT(LRSS,LRAA,LRAD,LRAN) ;  file results
 I '$D(LRSS) S JVOROUT(1)="-1^Error: Order was not succesfully accessioned." Q
 I LRSS="AU" G EXIT
 I LRSS="MI" D MI(LRAA,LRAD,LRAN) G EXIT
 I LRSS="CY" D APRSLT^JVHLLAP(LRSS,LRAA,LRAD,LRAN) G EXIT
 I LRSS="AP" G EXIT
 I LRSS="SP" D APRSLT^JVHLLAP(LRSS,LRAA,LRAD,LRAN)
 Q
 ;
MI(LRAA,LRAD,LRAN) ;  file microbiology reports
 S LRCMT="",LRI=1
 S LR6207(23)="",LROK=0 ; rhl  LR6207 is an array of LAB(62.07 entries that have been implmmented
 ; LRDFN = Internal entry in LR( that is being worked on.
 S LRFILE=68.02,LRIEN=LRAN_","_LRAD_","_LRAA_","
 S LRDFN=$$GET1^DIQ(LRFILE,LRIEN,.01,"I")
 S LRIDT=$$GET1^DIQ(LRFILE,LRIEN,13.5,"I")
 S LRCDT=$$GET1^DIQ(LRFILE,LRIEN,9,"I")
 S LRFILE=63.05,LRIEN=LRIDT_","_LRDFN_","
 I '$$GET1^DIQ(LRFILE,LRIEN,.01,"I") S JVOROUT(1)="-1^Error: No entry in file (#63) for this patient" Q
 ; Pick up specimen
 S LRFILE=68.05,LRSPEC=$$GET1^DIQ(LRFILE,LRIEN,.01,"I")
 S LRCMT=""
 S X=HLARY("ORC",1),RSITE=$P($G(X),D1,13) K X
 I RSITE="" S RSITE=0
 I RSITE D
 .S LRFILE=4,LRIEN=RSITE_",",SITE=$$GET1^DIQ(LRFILE,LRIEN,.01,"I")
 .I SITE="" S RSITE=0
 S X=HLARY("OBR",1),LRTS=$P($P(X,D1,4),D2)
 S LRTSPN=$P($P(X,D1,46),D2)
 I LRTSPN'="" S LRTS=LRTSPN
 I LRTSPN="" S LRTS=$P($P(X,D1,4),D2)
 S LRFILE=68.04,LRIEN=LRTS_","_LRAN_","_LRAD_","_LRAA_","
 I '$$GET1^DIQ(LRFILE,LRIEN,.01,"I") S JVOROUT(1)="-1^Error: No tests associated with this accession" Q
 S Y=$$NOW^XLFDT
 ; Update Accession file with technologist ID and timestamp
 S LRFILE=68.04,LRIEN=LRTS_","_LRAN_","_LRAD_","_LRAA_","
 S LRFDA(4,LRFILE,LRIEN,3)=$G(JVTECH)
 S LRFDA(4,LRFILE,LRIEN,4)=$G(Y)
 D UPDATE^DIE("","LRFDA(4)","LRIENS","LRMSG")
 S ^LRO(68,LRAA,1,LRAD,1,"AD",$P(Y,"."),+LRAN)="",^LRO(68,LRAA,1,LRAD,1,"AC",Y,+LRAN)=""
 ; Get Edit Code from Lab Test file (Bacteriology, Mycology, etc.)
 S LRFILE=60,LRIEN=LRTS_",",EXEC=$$GET1^DIQ(LRFILE,LRIEN,98,"I")
 I 'EXEC S JVOROUT(1)="-1^Error: Lab test is not configured correctly" Q
 ; Normalize the value.
 D GETEXEC^JVHLLR3(.EXEC)
 I EXEC D
 .; Get the executable file numbers from the EXECUTE FILE, (#62.07)
 .S LRFILE=62.07,LRIEN=EXEC_",",LRTX(LRI)=$$GET1^DIQ(LRFILE,LRIEN,1,"I")
 .; Set the file node using the LRTX(LRI)
 .S LRSB=$S(LRTX(LRI)["11.5":1,LRTX(LRI)["23":11,LRTX(LRI)["19":8,LRTX(LRI)["15":5,LRTX(LRI)["34":16,1:"")
 .D @("MI"_EXEC) ; RHL 20100809
 I LROK=0 S JVOROUT(1)="-1^UNSUCESSFUL MICROBIOLOGY RESULTS SAVE"
 I LROK=1 D
 .S TIM=$$NOW^XLFDT
 .S LRFILE=100,LRIEN=ORDNM_",",LRD=$$GET1^DIQ(LRFILE,LRIEN,33,"I")
 .S LRORD=$P($G(LRD),";"),LRODT=$P($G(LRD),";",2),LRSN=$P($G(LRD),";",3)
 .S LRFILE=69.01,LRIEN=LRSN_","_LRODT_","
 .S LRFDA(3,LRFILE,LRIEN,20)=$G(TIM)
 .S LRFDA(3,LRFILE,LRIEN,21)=$$NOW^XLFDT
 .D UPDATE^DIE("","LRFDA(3)","LRIENS","LRMSG")
 .S LRFILE=68.02,LRIEN=LRAN_","_LRAD_","_LRAA_","
 .S LRSITE=$$GET1^DIQ(LRFILE,LRIEN,16.1,"I")
 .S LRDFN=$$GET1^DIQ(LRFILE,LRIEN,.01,"I")
 .S X=$$GET1^DIQ(LRFILE,LRIEN,12,"I")
 .I X="" S LRFDA(3,LRFILE,LRIEN,12)=$G(TIM)
 .S LRFDA(3,LRFILE,LRIEN,13)=$$NOW^XLFDT
 .D UPDATE^DIE("","LRFDA(3)","LRIENS","LRMSG")
 .S ^LR(LRDFN,"MI",LRIDT,"RF")=LRSITE
 .S ^LR(LRDFN,"MI",LRIDT,"ORU")=^LRO(68,LRAA,1,LRAD,1,LRAN,.3)
 .S LRFILE=100,LRIEN=ORDNM_","
 .S DATA=$$GET1^DIQ(LRFILE,LRIEN,33,"I")
 .I DATA'[LRSS D
 ..S LRFDA(4,LRFILE,LRIEN,33)=DATA_";"_LRSS_";"_LRIDT
 ..D UPDATE^DIE("","LRFDA(4)","LRIENS","LRMSG")
 .I $G(LRSTS)="F" D
 ..S DIE="^OR(100,",DA=ORDNM,ST=2,DR="22////"_$$NOW^XLFDT_";5////"_ST_";" D ^DIE K DIE,DA
 .S JVOROUT(1)=ORDNM_"^RS"
 K HLARY,LRSB,LRTS,LRTX,LRSPEC,REC
 Q
 ; ==================================================================
MI7 ; BACTERIOLOGY
 N DA,DIE,X,X1,X2,LR1,LRFDA,LRFILE,LRIEN,LRPRV,LRIENS,LRMSG
 N RCNT,ORGC,ORG,OBR,OBX,XCNT,ORGNM,CNT,LRANT,LRSUSC,LRRS1,LRRS2
 N LRRS3,LRORG,LRCCNT,LRGRAM,LRIEN,GRMARY,GRMCNT,NUM,NX,ORGIEN,QUIT
 ; File 1 node of Bacteriology results
 S X=HLARY("OBX",1),LRSTS=$P(X,D1,11),LRCMT=""
 S LRFILE=63.05,LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(1,LRFILE,LRIEN,11)=$G(LRCDT)
 S LRFDA(1,LRFILE,LRIEN,11.5)=$G(LRSTS)
 S LRFDA(1,LRFILE,LRIEN,11.55)=$G(JVTECH)
 D UPDATE^DIE("","LRFDA(1)","LRIENS","LRMSG")
 K LRFDA,LRIEN,LRIENS,LRMSG
 S LRIEN=LRIDT_","_LRDFN_",",LRFILE=63.05
 S LRFDA(99,LRFILE,LRIEN,.99)=$G(LRCMT)
 D UPDATE^DIE("","LRFDA(99)","LRIENS","LRMSG")
 ;
 ;  build ORG array of organism  e.g.  ORG(1)=500714\STREPTOCOCCUS SPECIES^<COLONY COUNT>^<GRAM STAIN>
 ;                                      ^piece-1 = ien\organism name
 ;                                      ^piece-2 = colony count, if any
 ;                                      ^piece-3 = gram stain, if any
 ;                                      ORG(1,"PENICILLIN")=<susceptability>
 ; ; CULTURE SUSCEPTIBILTY
 S RCNT=0,ORGC=0,GRMCNT=0
 S (LR1,ORGC,GRMCNT,NX)=0,(VAL,LNC)=""
 F  S NX=$O(HLARY("OBX",NX)) Q:'NX  D
 .S X=HLARY("OBX",NX)
 .I $P(X,D1,2)["CE" D  Q
 ..; Organism
 ..I $P(X,D1,3)["99VA61.2" D
 ...S VAL=$P(X,D1,4),ORGC=ORGC+1
 ...S ORG(VAL,ORGC)=$P($P(X,D1,3),D2,1,2)_D1
 ...S ORGNARY(+ORG(VAL,ORGC))="" Q
 .I $P(X,D1,2)["ST" D
 ..I $P(X,D1,3)["COLONY COUNT" D
 ...S VAL=$P(X,D1,4),LRCCNT(VAL)=$P(X,D1,5)
 ..I $P(X,D1,3)["SUSC" D
 ...S VAL=$P(X,D1,4)
 ...S LNC=$P($P(X,D1,3),D2,1),LRSUSC=$P(X,D1,8)
 ...S LRRS1(VAL,LNC)=LRSUSC
 ..; get BACT RPT REMARKS
 ..I $P(X,D1,3)["93928.0000" D
 ...S LR1=LR1+1,LRRS2(LR1)=$P(X,D1,5)
 ..; get gram stain
 ..I $P(X,D1,3)["664-3" D
 ...S GRMCNT=GRMCNT+1,GRMARY(GRMCNT)=$P(X,D1,5)
 ..I $P(X,D1,3)["87754.0000" D
 ...S GRMCNT=GRMCNT+1,GRMARY(GRMCNT)=$P(X,D1,5)
 ;
 ; NODE-4, FIELD-13 - BACT RPT REMARK -- FILE63.33
 I $D(LRRS2) D
 .S X1="" F  S X1=$O(LRRS2(X1)) Q:X1=""  D
 ..S RSLT=$$GET1^DIQ(63.33,X1_","_LRIDT_","_LRDFN_",",.01,"I")
 ..I LRRS2(X1)=$G(RSLT) Q
 ..S LRIEN="+"_X1_","_LRIDT_","_LRDFN_","
 ..S LRFDA(4,63.33,LRIEN,.01)=$G(LRRS2(X1))
 ..D UPDATE^DIE("","LRFDA(4)","LRIENS","LRMSG")
 K LRFDA,LRIENS,LRMSG,LRFILE,LRIEN
 ;
 ; file gram stain
 I $D(GRMARY) D
 .S X1="" F  S X1=$O(GRMARY(X1)) Q:X1=""  D
 ..S LRFILE=63.29,LRIEN="+"_X1_","_LRIDT_","_LRDFN_","
 ..S RSLT=$$GET1^DIQ(LRFILE,X1_","_LRIDT_","_LRDFN_",",.01,"I")
 ..I GRMARY(X1)=$G(RSLT) Q
 ..S LRFDA(2,LRFILE,LRIEN,.01)=$G(GRMARY(X1))
 ..D UPDATE^DIE("","LRFDA(2)","LRIENS","LRMSG")
 ..K LRFDA,LRIEN,LRMSG,LRIENS
 ;
 ; NODE-3, FIELD-12 - ORGANISM -- FILE 63.3
 N FLG
 S (NUM,DA)=0
 ; file organism
 S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,3,0)),D1,3)
 I QUIT'="" D
 .F I=1:1:QUIT  D
 ..S LRFILE=63.3,LRIEN=I_","_LRIDT_","_LRDFN_","
 ..S ORGN=$$GET1^DIQ(LRFILE,LRIEN,.01,"I") Q:ORGN=""
 ..I $D(ORGNARY(ORGN)) S ORGARY(ORGN)=I
 ..I '$D(ORGNARY(ORGN)) S LRFDA(3,LRFILE,LRIEN,.01)="@" D UPDATE^DIE("","LRFDA(3)","LRIENS","LRMSG")
 S VAL="" F  S VAL=$O(ORG(VAL)) Q:VAL=""  D
 .S ORGC=0 F  S ORGC=$O(ORG(VAL,ORGC)) Q:ORGC=""  D
 ..S X1=ORGC,NUM=0
 ..S X=ORG(VAL,ORGC),LRORG=+X,LRCCNT=$G(LRCCNT(VAL))  ;BL;ADDED $G TO STOP M ERROR WHERE LRCCNT(VAL) UNDEFINED
 ..; Check if organism is already in file (#63)
 ..; Do not file organsim if it's already in file (#63)
 ..I '$D(ORGARY(LRORG)) D
 ...S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,3,0)),D1,3) I QUIT="" S QUIT=1
 ...S LRFILE=63.3,LRIEN="+"_QUIT_","_LRIDT_","_LRDFN_","
 ...S LRFDA(3,LRFILE,LRIEN,.01)=$G(LRORG)
 ...S LRFDA(3,LRFILE,LRIEN,1)=$G(LRCCNT(VAL))_" "
 ...D UPDATE^DIE("","LRFDA(3)","LRIENS","LRMSG")
 ...S NUM=$P($G(^LR(LRDFN,"MI",LRIDT,3,0)),D1,3)
 ...K LRFDA,LRIENS,LRMSG,LRIEN
 ...; file antibiotic and susc
 ..S LNC="" F  S LNC=$O(LRRS1(VAL,LNC)) Q:LNC=""  D
 ...I 'NUM S NUM=ORGARY(LRORG)
 ...S LRSUSC=LRRS1(VAL,LNC)
 ...S LNCARY=$$HL2LAH^LA7VHLU6(LNC,"","LN",2.14,"","MI")
 ...S LRCODE=$P(LNCARY,D1,4)
 ...Q:LRCODE=""
 ...I LRCODE'["." D
 ....S LRCODE=+$P(^DD(63.3,LRCODE,0),D1,4)
 ...Q:LRCODE=""
 ...S ^LR(LRDFN,"MI",LRIDT,3,NUM,LRCODE)=LRSUSC
 ...S SUSCARY(LRCODE)=LRSUSC
 ..S LRCODE=0 F  S LRCODE=$O(^LR(LRDFN,"MI",LRIDT,3,NUM,LRCODE)) Q:'LRCODE  D
 ...Q:LRCODE'>1
 ...I '$D(SUSCARY(LRCODE)) S ^LR(LRDFN,"MI",LRIDT,3,NUM,LRCODE)="^^"
 ..K SUSCARY
 S LROK=1
 Q
 ;
MI8 ; TB BACTERIOLOGY
 N FLG,LR1,LR2,LR3,NX,LRIEN,LRRS1,LRRS1,LRRS3,LRMYC,LRCMT,LRQTY
 N MYCC,MYCARY,MYARY,NUM,SUSCARY,QUIT,X1,X2,X,XCT,XX
 ; File 1 node of TB Bacteriology results
 S X=HLARY("OBX",1),LRSTS=$P(X,D1,11),LRCMT=""
 S LRFILE=63.05,LRIEN=LRIDT_","_LRDFN_","
 S LRFDA(11,LRFILE,LRIEN,22)=$G(LRCDT)
 S LRFDA(11,LRFILE,LRIEN,23)=$G(LRSTS)
 S LRFDA(11,LRFILE,LRIEN,25)=$G(LRQTY)
 S LRFDA(11,LRFILE,LRIEN,25.5)=$G(JVTECH)
 D UPDATE^DIE("","LRFDA(11)","LRIENS","LRMSG")
 K LRFDA,LRIEN,LRIENS,LRMSG
 S LRIEN=LRIDT_","_LRDFN_",",LRFILE=63.05
 S LRFDA(99,LRFILE,LRIEN,.99)=$G(LRCMT)
 D UPDATE^DIE("","LRFDA(99)","LRIENS","LRMSG")
 S (LR1,LR2,LR3,LR4,MYCC)=0
 S NX="" F  S NX=$O(HLARY("OBX",NX)) Q:'NX  D
 .S X=HLARY("OBX",NX)
 .I $P(X,D1,2)="CE" D
 ..S VAL=$P(X,D1,4),MYCC=MYCC+1
 ..S LIEN=$P($P(X,D1,3),D2,4),MYC(VAL,MYCC)=LIEN
 ..S MYCOARY(LIEN)=""
 .I $P(X,D1,2)="ST" D
 ..I $P(X,D1,3)["COLONY COUNT" D
 ...S VAL=$P(X,D1,4),LRRS1(VAL)=$P(X,D1,5)
 ..I $P(X,D1,3)["SUSC" D
 ...S ANT=$P($P(X,D1,3),D2,2),ANT=$P(ANT,":")
 ...S SUBSC="",SUBSC=$O(^DD(63.39,"B",ANT,SUBSC))
 ...S VAL=$P(X,D1,4),LRSUSC=$P(X,D1,8)
 ...S LRRS2(VAL,SUBSC)=LRSUSC
 ..I $P(X,D1,3)[87860.0000 D
 ...S LR3=LR3+1,LRCMT=$P(X,D1,5),LRRS3(LR3)=LRCMT
 ..I $P(X,D1,3)[11545 D
 ...S LRACFST=$P(X,D1,5),LR4=LR4+1,LRRS4(LR4)=LRACFST
 ;
 ; NODE-12, FIELD-26 -- 63.39 MYCOBACTERIUM
 ; NODE-12, FIELD-2 -- 63.4 COMMENT
 S NUM=0
 S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,12,0)),D1,3)
 I QUIT'="" D
 .F I=1:1:QUIT  D
 ..S LRIEN=I_","_LRIDT_","_LRDFN_",",LRFILE=63.39
 ..S MYCO=$$GET1^DIQ(63.39,LRIEN,.01,"I") Q:MYCO=""
 ..I $D(MYCOARY(MYCO)) S MYARY(MYCO)=I
 ..I '$D(MYCOARY(MYCO)) S LRFDA(12,LRFILE,LRIEN,.01)="@" D UPDATE^DIE("","LRFDA(12)","LRIENS","LRMSG")
 S VAL="" F  S VAL=$O(MYC(VAL)) Q:VAL=""  D
 .S MYCC=0 F  S MYCC=$O(MYC(VAL,MYCC)) Q:MYCC=""  D
 ..S X1=MYCC,NUM=0
 ..S X=MYC(VAL,MYCC),LRORG=+X,LRCCNT=LRRS1(VAL)
 ..I '$D(MYARY(LRORG)) D
 ...S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,12,0)),D1,3) I QUIT="" S QUIT=1
 ...S LRIEN="+"_QUIT_","_LRIDT_","_LRDFN_",",LRFILE=63.39
 ...S LRFDA(12,LRFILE,LRIEN,.01)=$G(MYC(VAL,MYCC))
 ...S LRFDA(12,LRFILE,LRIEN,1)=$G(LRRS1(VAL))
 ...D UPDATE^DIE("","LRFDA(12)","LRIENS","LRMSG")
 ...S NUM=$P($G(^LR(LRDFN,"MI",LRIDT,12,0)),D1,3)
 ..S LNC="" F  S LNC=$O(LRRS2(VAL,LNC)) Q:LNC=""  D
 ...I 'NUM S NUM=MYARY(LRORG)
 ...S LRSUSC=LRRS2(VAL,LNC)
 ...S ^LR(LRDFN,"MI",LRIDT,12,NUM,LNC)=LRSUSC
 ...S SUSCARY(LNC)=LRSUSC
 ..S LRCODE=0 F  S LRCODE=$O(^LR(LRDFN,"MI",LRIDT,12,NUM,LRCODE)) Q:'LRCODE  D
 ...Q:LRCODE'>1
 ...I '$D(SUSCARY(LRCODE)) S ^LR(LRDFN,"MI",LRIDT,12,NUM,LRCODE)="^^"
 ..K SUSCARY
 ;
 ; NODE-13, FIELD-27- -- 63.41 TB RPT REMARK
 S X1="" F  S X1=$O(LRRS3(X1)) Q:X1=""  D
 .S LRFILE=63.41,LRIEN=X1_","_LRIDT_","_LRDFN_","
 .S RSLT=$$GET1^DIQ(LRFILE,LRIEN,.01,"I")
 .I LRRS3(X1)=$G(RSLT) Q
 .S LRIEN="+"_X1_","_LRIDT_","_LRDFN_",",LRFILE=63.41
 .S LRFDA(13,LRFILE,LRIEN,.01)=$G(LRRS3(X1))
 .D UPDATE^DIE("","LRFDA(13)","LRIENS","LRMSG")
 ; File Acid Fast Stain result
 S X1="" F  S X1=$O(LRRS4(X1)) Q:X1=""  D
 .S LRFILE=63.05,LRIEN=LRIDT_","_LRDFN_","
 .S LRFDA(11,LRFILE,LRIEN,24)=$G(LRRS4(1))
 .D UPDATE^DIE("","LRFDA(11)","LRIENS","LRMSG")
 S LROK=1
 Q
 ;
MI9 ; MYCOLOGY -- 9
 N FLG,LR1,LR2,L3,LRIEN,LRFILE,LRRS1,LRRS2,LRRS3,LRSMP,QUIT,X,X1
 S X=HLARY("OBX",1),LRSTS=$P(X,D1,11),LRCMT=""
 S LRIEN=LRIDT_","_LRDFN_",",LRFILE=63.05
 S LRFDA(8,LRFILE,LRIEN,18)=LRCDT
 S LRFDA(8,LRFILE,LRIEN,19)=LRSTS
 S LRFDA(8,LRFILE,LRIEN,19.5)=$G(JVTECH)
 D UPDATE^DIE("","LRFDA(8)","LRIENS","LRMSG")
 K LRFDA,LRIENS,LRMSG
 S LRIEN=LRIDT_","_LRDFN_",",LRFILE=63.05
 S LRFDA(99,LRFILE,LRIEN,.99)=$G(LRCMT)
 D UPDATE^DIE("","LRFDA(99)","LRIENS","LRMSG")
 ;
 S (LR1,LR2,LR3)=0
 ; Capture the Fungas IEN
 S NX="" F  S NX=$O(HLARY("OBX",NX)) Q:'NX  D
 .S X=HLARY("OBX",NX)
 .I $P(X,D1,2)="CE" D
 ..S LIEN=$P($P(X,D1,3),D2)
 ..S LR1=LR1+1,LRRS1(LR1)=LIEN,FNGARY(LIEN)=""
 .I $P(X,D1,2)="ST" D
 ..S XX=$P(X,D1,3) D
 ...I XX["93974.0000" S LR2=LR2+1,LRMCMT=$P(X,D1,5),LRRS2(LR2)=LRMCMT ;Mycology Rpt Remark
 ...I XX["93984.0000" S LR3=LR3+1,LRSMP=$P(X,D1,5),LRRS3(LR3)=LRSMP ; Mycology Smear/Prep
 ; NODE-9, FIELD-63.37 FUNGUS/YEAST
 S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,9,0)),D1,3)
 I QUIT'="" D  ; If fungus is new, file it, if it's there do not file.
 .F I=1:1:QUIT  D
 ..S LRIEN=I_","_LRIDT_","_LRDFN_",",LRFILE=63.37
 ..S FUNG=$$GET1^DIQ(63.37,LRIEN,.01,"I") Q:FUNG=""
 ..I $D(FNGARY(FUNG)) S FNARY(FUNG)=I
 ..I '$D(FNGARY(FUNG)) S LRFDA(9,LRFILE,LRIEN,.01)="@" D UPDATE^DIE("","LRFDA(9)","LRIENS","LRMSG")
 S X1="" F  S X1=$O(LRRS1(X1)) Q:X1=""  D
 .I '$D(FNARY(LRRS1(X1))) D
 ..S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,9,0)),D1,3) I QUIT="" S QUIT=1
 ..S LRIEN="+"_QUIT_","_LRIDT_","_LRDFN_",",LRFILE=63.37
 ..S LRFDA(9,LRFILE,LRIEN,.01)=$G(LIEN)
 ..D UPDATE^DIE("","LRFDA(9)","LRIENS","LRMSG")
 ; NODE-10, FIELD-21 MYCOLOGY RPT REMARK
 S X1="" F  S X1=$O(LRRS2(X1)) Q:X1=""  D
 .S LRFILE=63.38
 .S RSLT=$$GET1^DIQ(LRFILE,X1_","_LRIDT_","_LRDFN_",",.01,"I")
 .I LRRS2(X1)=$G(RSLT) Q
 .S LRIEN="+"_X1_","_LRIDT_","_LRDFN_","
 .S LRFDA(10,LRFILE,LRIEN,.01)=$G(LRRS2(X1))
 .D UPDATE^DIE("","LRFDA(10)","LRIENS","LRMSG")
 .K LRIENS,LRMSG
 ; NODE-15, FIELD 19.6 MYCOLOGY SMEAR/PREP
 S X1="" F  S X1=$O(LRRS3(X1)) Q:X1=""  D
 .S LRFILE=63.371
 .S RSLT=$$GET1^DIQ(LRFILE,X1_","_LRIDT_","_LRDFN_",",.01,"I")
 .I LRRS3(X1)=$G(RSLT) Q
 .S LRIEN="+"_X1_","_LRIDT_","_LRDFN_","
 .S LRFDA(15,LRFILE,LRIEN,.01)=$G(LRRS3(X1))
 .D UPDATE^DIE("","LRFDA(15)","LRIENS","LRMSG")
 .K LRIENS,LRMSG
 ;
 S LROK=1
 Q
 ;
MI10 ; PARISITOLOGY LRSB=10
 ;
 N N,DA,X,X2,X3,LRRS1,LRRS2,LRRS3,LRC1,LRC2,LRC3,QUIT
 ;File Provider, Lab test and Collection Date/Time in Lab Data file (#63)
 ; Get status from incoming message, OBX-11.  Final or Prelim.
 S X=HLARY("OBX",1),LRSTS=$P(X,D1,11),LRCMT=""
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFILE=63.05
 S LRFDA(5,LRFILE,LRIEN,14)=LRCDT
 S LRFDA(5,LRFILE,LRIEN,15)=LRSTS
 S LRFDA(5,LRFILE,LRIEN,15.5)=$G(JVTECH)
 D UPDATE^DIE("","LRFDA(5)","LRIENS","LRMSG")
 K LRFDA,LRIEN,LRIENS,LRMSG
 S LRIEN=LRIDT_","_LRDFN_",",LRFILE=63.05
 S LRFDA(99,LRFILE,LRIEN,.99)=$G(LRCMT)
 D UPDATE^DIE("","LRFDA(99)","LRIENS","LRMSG")
 ; Get the results from multiple OBX segments: Stage, Parasite remark, Parasite ID
 S LRC1=0,LRC2=0,LRC3=0,SUB=""
 S N=0 F  S N=$O(HLARY("OBX",N)) Q:'N  D
 .S X=HLARY("OBX",N),X3=$P($P(X,D1,3),D2,1) I X3="87505.0000" S SUB=$P(X,D1,4),LRRS1(SUB)=$P(X,D1,5) Q
 .S X=HLARY("OBX",N),X3=$P($P(X,D1,3),D2,1) I X3="93929.0000" S LRC2=LRC2+1,LRRS2(LRC2)=$P(X,D1,5) Q
 .S X=HLARY("OBX",N),X3=$P(X,D1,3) I X3["99VA61.2" D
 ..S LRC3=LRC3+1,SUB=$P(X,D1,4),LRRS3(SUB,LRC3)=$P($P(X,D1,5),D2),INCPARY(LRRS3(SUB,LRC3))="" Q
 ;
 ;16 File Parasite and stage of growth in Lab Data file (#63)
 S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,6,0)),D1,3)
 I QUIT'="" D
 .F I=1:1:QUIT  D
 ..S LRIEN=I_","_LRIDT_","_LRDFN_",",LRFILE=63.34
 ..S PARAS=$$GET1^DIQ(63.34,LRIEN,.01,"I") Q:PARAS=""
 ..I $D(INCPARY(PARAS)) S PARY(PARAS)=I
 ..I '$D(INCPARY(PARAS)) S LRFDA(6,LRFILE,LRIEN,.01)="@" D UPDATE^DIE("","LRFDA(6)","LRIENS","LRMSG")
 .; If no parasite is found in message, delete from file (#63).
 S NUM=0
 S VAL="" F  S VAL=$O(LRRS3(VAL)) Q:VAL=""  D
 .S X1="" F  S X1=$O(LRRS3(VAL,X1)) Q:X1=""  D
 ..S PARAS=LRRS3(VAL,X1),NUM=0
 ..I '$D(PARY(PARAS)) D
 ...S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,6,0)),D1,3) I QUIT="" S QUIT=1
 ...S LRIEN="+"_QUIT_","_LRIDT_","_LRDFN_",",LRFILE=63.34
 ...S LRFDA(6,63.34,LRIEN,.01)=PARAS
 ...D UPDATE^DIE("","LRFDA(6)","LRIENS","LRMSG")
 ...S NUM=$P($G(^LR(LRDFN,"MI",LRIDT,6,0)),"^",3)
 ..I $D(LRRS1) D  ;Stage of growth
 ...S LRFILE=63.35
 ...S STAGE=LRRS1(VAL),STAGE=$E(STAGE,1)
 ...I 'NUM S NUM=PARY(PARAS)
 ...S NUM1=$P($G(^LR(LRDFN,"MI",LRIDT,6,NUM,1,0)),"^",3)
 ...I NUM1="" S NUM1=1 D
 ....S LRIEN="+"_NUM1_","_NUM_","_LRIDT_","_LRDFN_",",LRFILE=63.35
 ....S LRFDA(6,63.35,LRIEN,.01)=$G(STAGE)
 ....D UPDATE^DIE("","LRFDA(6)","LRIENS","LRMSG")
 ...I NUM1'="" D
 ....S RSLT=$$GET1^DIQ(LRFILE,NUM1_","_NUM_","_LRIDT_","_LRDFN_",",.01)
 ....S RSLT=$E(RSLT,1) I RSLT=STAGE Q
 ....S LRIEN="+"_NUM1_","_NUM_","_LRIDT_","_LRDFN_",",LRFILE=63.35
 ....S LRFDA(6,63.35,LRIEN,.01)=$G(STAGE)
 ....D UPDATE^DIE("","LRFDA(6)","LRIENS","LRMSG")
 ;17   File Parasite Report Remark in Lab Data file (#63)
 I $D(LRRS2) D
 .S X1="" F  S X1=$O(LRRS2(X1)) Q:X1=""  D
 ..S LRFILE=63.36
 ..S RSLT=$$GET1^DIQ(LRFILE,X1_","_LRIDT_","_LRDFN_",",.01,"I")
 ..I LRRS2(X1)=$G(RSLT) Q
 ..S LRIEN="+"_X1_","_LRIDT_","_LRDFN_","
 ..S LRFDA(7,63.36,LRIEN,.01)=$G(LRRS2(X1))
 ..D UPDATE^DIE("","LRFDA(7)","LRIENS","LRMSG")
 K LRIEN,LRFDA,LRFILE
 S LROK=1
 Q
 ;
MI16 ; VIROLOGY LRBS=16
 ;
 N LR1,LR2,LRIEN,LRFILE,LRRS1,LRRS2,NX,QUIT,X,XCT
 S X=HLARY("OBX",1),LRSTS=$P(X,D1,11),LRCMT="",LRCMT=""
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFILE=63.05
 S LRFDA(16,LRFILE,LRIEN,33)=LRCDT
 S LRFDA(16,LRFILE,LRIEN,34)=LRSTS
 S LRFDA(16,LRFILE,LRIEN,35)=$G(JVTECH)
 D UPDATE^DIE("","LRFDA(16)","LRIENS","LRMSG")
 K LRFDA,LRIEN,LRIENS,LRMSG
 S LRIEN=LRIDT_","_LRDFN_",",LRFILE=63.05
 S LRFDA(99,LRFILE,LRIEN,.99)=$G(LRCMT)
 D UPDATE^DIE("","LRFDA(99)","LRIENS","LRMSG")
 S (LR1,LR2)=0
 K LRRS1
 S NX="" F  S NX=$O(HLARY("OBX",NX)) Q:'NX  D
 .S X=HLARY("OBX",NX)
 .I $P(X,D1,2)="CE" S LR1=LR1+1 D
 ..S LRIEN=$P($P(X,D1,3),D2)
 ..S LRRS1(LR1)=LRIEN,VIRARY(LRIEN)=""
 .I $P(X,D1,2)="ST" S LR2=LR2+1 D
 ..S LRCMT=$P(X,D1,5)
 ..S LRRS2(LR2)=LRCMT
 ;
 S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,17,0)),D1,3)
 I QUIT'="" D
 .F I=1:1:QUIT  D
 ..S LRIEN=I_","_LRIDT_","_LRDFN_",",LRFILE=63.43
 ..S VIRUS=$$GET1^DIQ(LRFILE,LRIEN,.01,"I") Q:VIRUS=""
 ..I $D(VIRARY(VIRUS)) S VARY(VIRUS)=I
 ..I '$D(VIRARY(VIRUS)) S LRFDA(17,LRFILE,LRIEN,.01)="@" D UPDATE^DIE("","LRFDA(17)","LRIENS","LRMSG")
 S LR1="" F  S LR1=$O(LRRS1(LR1)) Q:'LR1  D
 .I '$D(VARY(LRRS1(LR1))) D
 ..S QUIT=$P($G(^LR(LRDFN,"MI",LRIDT,17,0)),D1,3) I QUIT="" S QUIT=1
 ..S LRIEN="+"_QUIT_","_LRIDT_","_LRDFN_",",LRFILE=63.43
 ..S LRFDA(17,LRFILE,LRIEN,.01)=$G(LRRS1(LR1))
 ..D UPDATE^DIE("","LRFDA(17)","LRIENS","LRMSG")
 ; Virology report remark
 S LR1="" F  S LR1=$O(LRRS2(LR1)) Q:'LR1  D
 .S RSLT=$$GET1^DIQ(63.44,LR1_","_LRIDT_","_LRDFN_",",.01,"I")
 .I LRRS2(LR1)=$G(RSLT) Q
 .S LRFILE=63.44,LRIEN="+"_LR1_","_LRIDT_","_LRDFN_","
 .S LRFDA(18,LRFILE,LRIEN,.01)=$G(LRRS2(LR1))
 .D UPDATE^DIE("","LRFDA(18)","LRIEN","LRMSG")
 S LROK=1
 Q
 ;
MI23 ; GRAM STAIN LABELED -- 23
 N LRRS1,N1,N2,S1,X1
 S S1=0
 S X=HLARY("OBX",1),LRSTS=$P(X,D1,11),LRCMT=""
 S LRIEN=LRIDT_","_LRDFN_","
 S LRFILE=63.05
 S LRFDA(1,LRFILE,LRIEN,11)=$G(LRCDT)
 S LRFDA(1,LRFILE,LRIEN,11.5)=$G(LRSTS)
 S LRFDA(1,LRFILE,LRIEN,11.55)=$G(JVTECH)
 D UPDATE^DIE("","LRFDA(1)","LRIENS","LRMSG")
 K LRFDA,LRIENS,LRMSG,LRFILE
 S N1="" F  S N1=$O(HLOBX(N1)) Q:N1=""  D
 .S LR4=$P($P(HLOBX(N1),D1,4),D2,1)
 .S LRFILE=60,LRIEN=LR4_",",LR4=$$GET1^DIQ(LRFILE,LRIEN,.01,"I")
 .S N2="" F  S N2=$O(HLOBX(N1,N2)) Q:'N2  D
 ..S S1=S1+1,LRRS1(S1)=$P(HLOBX(N1,N2),D1,5)
 S X1="" F  S X1=$O(LRRS1(X1)) Q:X1=""  D
 .S RSLT=$$GET1^DIQ(63.29,X1_","_LRIDT_","_LRDFN_",",.01,"I")
 .I LRRS1(X1)=$G(RSLT) Q
 .S LRIEN="+"_X1_","_LRIDT_","_LRDFN_","
 .S LRFDA(2,63.29,LRIEN,.01)=$G(LRRS1(X1))
 .D UPDATE^DIE("","LRFDA(2)","LRIENS","LRMSG")
 S LROK=1
 Q
 ;
EXIT ;
 K ANT,DATA,D1,D2,DC3,DR,EXEC,FLAG,FLD,FLG,FNARY,FNGARY,FUNG,HLOBX,I,INCPARY,K,LIEN,LR4,LR6207
 K LRACFST,LRBG0,LNC,LNCARY,LRCCNT,LRCDT,LRCODE,LRI,LRIDT,LRMIDEF,LRMIOTH,LRDFN,LRMCMT,JVTECH
 K LRODT,LROK,LRORD,LRORG,LRPTP,LRPRV,LRVPRV,LRRDT,LRRS1,LRRS2,LRRS3,LRRS4,LRSAMELRSPT
 K LRSN,LRSITE,LRSS,LRSTS,LRUNDO,LRD,LRVAL,LRVPRN,MYCARY,MYC,MYCC,MYCO,MYCOARY,NUM
 K NUM1,ORDNM,PAARY,PARARY,PARY,PARA,PARAS,ORGARY,ORGN,ORGNARY,RSITE,RSLT,SITE,ST,STAGE
 K SUB,SUBSC,SUSCARY,TIM,VA,VAL,VIRARY,VIRUS,VARY,X,X1,X2,X3,X5,XN,Y
 Q
 ;



